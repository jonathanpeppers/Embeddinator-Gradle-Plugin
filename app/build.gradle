import com.embeddinator.Embeddinator

apply plugin: 'com.android.application'

buildscript {
    repositories {
        maven {
            url uri('../repo')
        }
    }
    dependencies {
        classpath group: 'com.embeddinator',
                name: 'gradle',
                version: '1.0.0'
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.xamarin.embeddinator"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:design:25.3.1'
    testCompile 'junit:junit:4.12'
}

//TODO: figure out how to just use 'embeddinator'?
apply plugin: Embeddinator

task copyAssemblies  {
    def tempDir = new File(buildDir, 'generated/assemblies/')
    def destDir = new File(tempDir, '/')
    // THIS IS KEY: Add resDir as a resource directory so that it is
    //              automatically included in the APK
    android {
        sourceSets {
            main.resources {
                srcDir tempDir
            }
        }
    }
    doLast {
        copy {
            into destDir
            from 'src/main/assets/assemblies/'
        }
    }
}

project.afterEvaluate {
    tasks.findAll { task ->
        task.name.startsWith('merge') && task.name.endsWith('Resources')
    }.each { t -> t.dependsOn copyAssemblies }
}